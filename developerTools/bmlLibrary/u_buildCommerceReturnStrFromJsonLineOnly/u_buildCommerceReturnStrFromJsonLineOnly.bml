/**
@name Build Commerce Return Str From Json (Line Only)
@api u_buildCommerceReturnStrFromJsonLineOnly
@param data Json the JSON object containing the Commerce data
@return String a Commerce return string in the format: ```1~quoteNumber_t~123|2~aQuoteLineAttr_l~value|3~aQuoteLineAttr_l~value1```.
@package utils/Commerce
@revision
Rev. Date |Developer            |Notes / Comments
----------|---------------------|-------------------------------------------------------------------------
2018-10-18|michael.yeung        | skip all the header level attributes.
*/
C_DOC_NUM_KEY = "_document_number";
C_ITEMS = "items";
C_PREFIX_FOR_IGNORE_ATTRIBUTES = "fp_";

result = string[];

keys = jsonKeys(data);
for key in keys {
    if (key == C_ITEMS) {
        items = jsonPathGetMultiple(data, "$.items[*]");
        loopCtrl = range(jsonArraySize(items));
        for idx in loopCtrl {
            item = jsonArrayGet(items, idx, "json");
            docNum = jsonGet(item, C_DOC_NUM_KEY);
            attrKeys = jsonKeys(item);
            for attrKey in attrKeys {
                if (attrKey == C_DOC_NUM_KEY or startsWith(attrKey, C_PREFIX_FOR_IGNORE_ATTRIBUTES)) {
                    continue;
                }
                append(result, docNum + "~" + attrKey + "~" + jsonGet(item, attrKey, "string"));
            }
        }
    }
}

return join(result, "|");