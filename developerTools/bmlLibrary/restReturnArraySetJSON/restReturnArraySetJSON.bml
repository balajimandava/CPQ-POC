/**
@restReturnArraySetJSON
Rev. Date |Developer            |Notes / Comments
----------|---------------------|-------------------------------------------------------------------------
2019-11-29|Vijetha	            |Util to return Array set values as JSON. Built this util to fix the defect - INC000012958461  
2020-03-24|Atul Jain			|Removed commented code, and added debug mode to print statements.
*/
//Variable declaration and importing required attributes.
paramDict = inputDict;
transactionId = "";
arraySetVarName = "";
siteName = _company_name;
debugMode = false;
//Read input parameter dictionary and set corresponding local variables
if(containskey(paramDict,"BSID")){
	transactionId = get(paramDict,"BSID","string");
}
if(containskey(paramDict,"ArraySetVarName")){
	arraySetVarName = get(paramDict,"ArraySetVarName","string");//Array set name, which need to be returned.
}
if(containskey(paramDict,"SiteName")){
	siteName = get(paramDict,"SiteName","string");//Site name is already imported, however if site name is sent as param, then param input will override the imported value.
}
url = "https://"+siteName+".bigmachines.com/rest/v6/commerceDocumentsOraclecpqoTransaction/"+transactionId+"/"+arraySetVarName;
user = util.getSiteSpecificData(siteName);//using an existing util library to return a specific user and pwd for corresponding site.
login = jsonget(user, "FieldName", "string", "");
password = jsonget(user, "Value1", "string", "");
if (isnull(login) or isnull(password) or login == "" or password == "") {//Error handling if no user login and pwd available.
    throwerror("No API User is configured for this environment, please check data in Site_Specific data table", false);
}
if(transactionId == "" OR arraySetVarName == "" ){//Error handling if transactionId and array set name is not passed.
	throwerror("Transaction ID or Arrayset Var name cannot be empty", false);
}
//Prepare required header dictionary for REST API call.
headers = dict("string");
put(headers, "Content-Type", "application/json");
put(headers, "Authorization", "Basic " + encodebase64(login + ":" + password));
response = urldata(url, "GET", headers);//API call.
statusCode = get(response, "Status-Code");//Response code.
if (atoi(statusCode) >= 300) {//If response code > 200, it means API call failed. Error handling if API Call failed.
    throwerror("Required Info not available: " + url, false);
}
jsonResp = json(get(response, "Message-Body"));//Rest API gives response in JSON format. Extract Message Body 

if(debugMode)
{
	print jsonResp;
}
return jsonResp ;