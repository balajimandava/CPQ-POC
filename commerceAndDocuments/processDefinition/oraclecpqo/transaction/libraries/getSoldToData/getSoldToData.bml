/**
@name Get Sold To Data
@api getSoldToData
@summary 
@attribute _system_company_name
@attribute _system_user_login
@attribute eRecordID_t
@attribute isDistiQuote_t
@attribute isVendorQuote_t
@attribute ntDistributionChannel_t
@attribute ntSelectedSalesOrg_t
@attribute owner_t
@attribute qq_ntsalesOrg_t
@function Dictionary of String Dictionaries customerMasterInfo(String Dictionary inputDict)
@function String Dictionary getRegionsForSalesOrg(String[] salesOrgs)
@function String getStringDict(String Dictionary inputDict, String key, String default)
@return String
@revision
Rev. Date |Developer            |Notes / Comments
----------|---------------------|-------------------------------------------------------------------------
2018-01-04|Tat.Leung            |
2018-01-09|Michael.Yeung        |Display Sold to name/address and not hierachy name address
2018-02-12|Tat.Leung            |
2018-02-27|Tat.Leung            |Preload sales region in dict to improve perfornace
2018-04-19|michael.yeung        |
2018-06-04|michael.yeung        | Move delegate lookup to customerMasterInfo, use user selected Sales Org/distribution channel if available (i.e. after user made selection on those on the start page)
2018-06-05|michael.yeung        |
2018-06-08|michael.yeung        |
2018-06-09|michael.yeung        |
*/
//  37678048

retSoldToRecordsData = "";
colSep = "$,$";
rowSep = "#@#";

salesOrgStr = qq_ntsalesOrg_t;

salesOrgArr = split(salesOrgStr, rowSep);

eRecordID = eRecordID_t;

eRecordIDs = string[] {
    eRecordID
};

regionLookup = util.getRegionsForSalesOrg(salesOrgArr);

useSelectedSalesOrg = false;
if( ntSelectedSalesOrg_t<>"" AND ntDistributionChannel_t<>"") {
    useSelectedSalesOrg = true;
    salesOrgArr = string[] {ntSelectedSalesOrg_t};
}

for salesOrg in salesOrgArr {
    print "-----------------> salesOrg = " + salesOrg;
    inputMasterInfo = dict("string");

    if (isDistiQuote_t) {
        append(eRecordIDs, _system_company_name);
    } elif (isVendorQuote_t) {
        append(eRecordIDs, substring(_system_company_name,1));
    }

    put(inputMasterInfo, "eRecordID", join(eRecordIDs, rowSep));
    put(inputMasterInfo, "SalesOrg", salesOrg);
    put(inputMasterInfo, "LoginID", owner_t);
    put(inputMasterInfo, "IsDistiQuote", string(isDistiQuote_t));
    put(inputMasterInfo, "IsVendorQuote", string(isVendorQuote_t));
    put(inputMasterInfo, "selectedSalesOrg", ntSelectedSalesOrg_t);
    put(inputMasterInfo, "selectedDistributionChannel", ntDistributionChannel_t);

    customerData = util.customerMasterInfo(inputMasterInfo);

    if( useSelectedSalesOrg ) {
        salesOrg = ntSelectedSalesOrg_t;
    }

    if (containskey(customerData, "hierarchyDict")) {

        hierarchyDict = get(customerData, "hierarchyDict");
        allkeys = keys(hierarchyDict);
        allKeysSorted = sort(allKeys, "asc", "text");

        //HQ$,$L1$,$L2$,$L3$,$Name1$,$PSTLZ$,$STRAS

        soldToId = string[];

        for eachID in allKeysSorted {
            hqNumber = "";
            lastLevel = "";
            splitIds = split(eachID, ".");
            maxCNT = sizeofarray(splitIDs);
            hqNumber = splitIds[0];
            lastLevel = splitIds[maxCNT - 1];

            if (maxCNT == 3 OR maxCNT == 2 or maxCNT == 1) { // Entry containing non-blank Sold Tos Only.
                soldToNumber = splitIds[0];
                if( maxCNT > 1) {
                    soldToNumber = splitIds[1];
                }
                if (findinarray(soldToId, salesOrg + "-" + soldToNumber) == -1) {
                    append(soldToId, salesOrg + "-" + soldToNumber);
                    soldToRecordRow = string[];

                    hqCustInfoDict = get(customerData, hqNumber);
                    hqName1 = util.getStringDict(hqCustInfoDict, "NAME1", "");
                    append(soldToRecordRow, salesOrg); // Sales Org
                    append(soldToRecordRow, hqNumber); // Hierarchy Number
                    append(soldToRecordRow, soldToNumber); // Sold To Number

                    idx = 1;
                    if (maxCNT == 1) {
                        idx = 0;
                    }

                    llSoldToInfoDict = get(customerData, soldToNumber);
                    soldToType = util.getStringDict(llSoldToInfoDict, "KTOKD", "");
                    if (soldToType == "Z012"
                        and maxCNT >= 2) { // 2018/01/09 MY original code was showing the higher hierarchy name and address which is wrong for US
                        llSoldToInfoDict = get(customerData, soldToNumber);
                    }

                    append(soldToRecordRow, util.getStringDict(llSoldToInfoDict, "NAME1", "")); // Sold To Name
                    append(soldToRecordRow, util.getStringDict(llSoldToInfoDict, "PSTLZ", "")); // Postal Code
                    append(soldToRecordRow, util.getStringDict(llSoldToInfoDict, "ORT01", "")); // City
                    countryCode = util.getStringDict(llSoldToInfoDict, "LAND1", ""); // Country Code
                    regionCode = util.getStringDict(llSoldToInfoDict, "REGIO", ""); // Region Code 

                    crKey = countryCode + "_" + regionCode;
                    regn = get(regionLookup, crKey);
                    if (isnull(regn)) {
                    	regn = "";
                    }
                    

                    if (regn == "ES-EMEA"
                        OR regn == "CH-EMEA") {
                        regn = "";
                    }
                    append(soldToRecordRow, regn); // Region 

                    if (retSoldToRecordsData <> "") {
                        retSoldToRecordsData = retSoldToRecordsData + rowSep;
                    }
                    retSoldToRecordsData = retSoldToRecordsData + join(soldToRecordRow, colSep);
                }
            }
        }
    }
    if( useSelectedSalesOrg ) {
        break;
    }
}


return retSoldToRecordsData;