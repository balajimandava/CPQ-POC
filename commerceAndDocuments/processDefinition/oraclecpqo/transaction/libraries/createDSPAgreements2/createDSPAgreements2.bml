/**********************************************************************************************************

Date		Name		Comments
27-Nov-2020	Rahul		Refer transactionName_t attribute from existingCommerce process
************************************************************************************************************/
startDate = getdate();
siteID = _system_company_name;  //Expected value is eatontest2, eatontest4 etc.
id = "";
tableName_t = "DSP_Agreements_2511";

//API Acess
apiUserID= "";
apiPassID= "";

salesENum="";
name1= "";
telf1= "";
email1="";
eRecID="";
eNumber = "";
validFrom = "";
validTo = "";
title = "";

// API Username and password is stroed in Site_Specific table so calling that table to fetch required information.
siteBMQL= bmql("SELECT FieldName,Value1 FROM Site_Specific WHERE SiteID = $siteID");
for eachSite in siteBMQL {
	apiUserID= get(eachSite,"FieldName");
	apiPassID= get(eachSite,"Value1");
}

//Basic authentication required in WebService API call.
auth_val = "Basic " + encodebase64(apiUserID + ":" + apiPassID);

resultSet = bmql ("SELECT TRANS_NAME,KUNNR,KTOKD,VKORG,VTWEG,SPART,DATAB,DATBI,TRANS_TYPE,KSCHL,MVGR5,KBETR,KONWA,MATNR,ENUMBER,CURRENCY,SCALE_FRM,SCALE_TO FROM $tableName_t");
//print resultSet;

transactionsJsonObj = json();

for row in resultSet {
	materialScale = 0;
	rowJsonObj = json();
	materialScaleJSON = json();
	materialScaleAdNFAJson = json();
	priceGrpAndDisc = json();
	materialsJsonArr = jsonarray();
	materialsPriceJsonArr = jsonarray();
	PriceGroupsJsonArr = jsonarray();
	PriceGroupsDscntJsonArr = jsonarray();
	
	materialArr = string[];
	mpgArr = string[];
	
	eNumber = get(row, "ENUMBER");
	
	//Get sales data from CUSMAS table and set it into different attribute for further use.		
	if(salesENum == ""){
		salesRS=BMQL("SELECT SORT2, VKORG, SMTP_ADDR, NAME1, TELF1, KUNNR FROM CUSMAS WHERE SORT2=$eNumber");
		for eachUser in salesRS{		
			salesENum=get(eachUser, "SORT2");
			name1 = get(eachUser,"NAME1");
			telf1 = get(eachUser,"TELF1");
			email1 = get(eachUser, "SMTP_ADDR");
			eRecID = get(eachUser, "KUNNR");		
		}
		
		salesUserRS = bmql("Select Title from SalesTeams where eNumber=$eNumber");
		for salesUser in salesUserRS{
			title = get(salesUser, "Title");
		}
	}
	
	transName = get(row, "TRANS_NAME");
	customerNum = get(row, "KUNNR");
	validFromDate = get(row, "DATAB");
	validToDate = get(row, "DATBI");
	
	scaleFromArr = split(get(row, "SCALE_FRM"),".");
	scaleToArr = split(get(row, "SCALE_TO"), ".");
	
	scaleFrom = scaleFromArr[0];
	scaleTo = scaleToArr[0];
	
	jsonKey = customerNum + "|" + validFromDate + "|" + validToDate;
	
	if (NOT isnull(jsonget(transactionsJsonObj, jsonKey, "json"))){
		rowJsonObj = jsonget(transactionsJsonObj, jsonKey, "json");
		
		if(jsonarraysize(jsonget(rowJsonObj, "MaterialNumbers", "jsonarray")) > 0){
			materialsJsonArr 	= jsonget(rowJsonObj, "MaterialNumbers", "jsonarray");
			materialsPriceJsonArr 	= jsonget(rowJsonObj, "MaterialPrice", "jsonarray");
		}
		
		if(jsonarraysize(jsonget(rowJsonObj, "PriceGroup", "jsonarray")) > 0){
			PriceGroupsJsonArr 	= jsonget(rowJsonObj, "PriceGroup", "jsonarray");
			PriceGroupsDscntJsonArr = jsonget(rowJsonObj, "PriceGroupDiscount", "jsonarray");
		}
		
		if(NOT isjsonnull(rowJsonObj, "MaterialScale")){
			materialScaleJSON = jsonget(rowJsonObj, "MaterialScale", "json");
		}
		
		if(NOT isjsonnull(rowJsonObj, "MaterialScaleAndNFA")){
			materialScaleAdNFAJson = jsonget(rowJsonObj, "MaterialScaleAndNFA", "json");
		}
		
		if(NOT isjsonnull(rowJsonObj, "PriceGrpAndDiscount")){
			priceGrpAndDisc = jsonget(rowJsonObj, "PriceGrpAndDiscount", "json");
		}
		
		if(NOT isjsonnull(rowJsonObj, "MatArrStr")){
			materialArr = split(jsonget(rowJsonObj, "MatArrStr", "string"), ",");
		}
		
		if(NOT isjsonnull(rowJsonObj, "MPGArrStr")){
			mpgArr = split(jsonget(rowJsonObj, "MPGArrStr", "string"), ",");
		}
	}
	
	jsonput(rowJsonObj, "TableName", tableName_t);
	jsonput(rowJsonObj, "TransactionName", transName);
	jsonput(rowJsonObj, "CustomerNumber", customerNum);
	jsonput(rowJsonObj, "CUST_TYPE", get(row, "KTOKD"));
	jsonput(rowJsonObj, "SalesOrg", get(row, "VKORG"));
	jsonput(rowJsonObj, "DistributionChannel", get(row, "VTWEG"));
	jsonput(rowJsonObj, "Division", get(row, "SPART"));
	jsonput(rowJsonObj, "FromDate", validFromDate);
	jsonput(rowJsonObj, "ToDate", validToDate);
	jsonput(rowJsonObj, "CURRENCY", get(row, "CURRENCY"));
	jsonput(rowJsonObj, "TRANS_TYPE", get(row, "TRANS_TYPE"));
	
	lineType = get(row, "KSCHL");
	if(lineType == "ZPR5"){
		materialNr = get(row, "MATNR");
		materialPrice = get(row, "KBETR");
		materialScale =	jsonget(materialScaleJSON, materialNr, "integer", 0);
		
		if(materialScale > 0){
			jsonput(materialScaleJSON, materialNr, materialScale+1);
		}else{
			jsonput(materialScaleJSON, materialNr, 1);
		}
		
		if(scaleFrom <> "" OR scaleTo <> ""){
			scalePriceJson = json();			
			matArray = jsonkeys(materialScaleAdNFAJson);
			if (findinarray(matArray, materialNr) <> -1){
				scalePriceJson = jsonget(materialScaleAdNFAJson, materialNr, "json");
			}
			jsonput(scalePriceJson, scaleFrom + "~$~" + scaleTo, materialPrice);
			jsonput(materialScaleAdNFAJson, materialNr, scalePriceJson);
		}
		else{
			jsonput(materialScaleAdNFAJson, materialNr, materialPrice);
		}
		
		jsonarrayappend(materialsJsonArr, materialNr);
		jsonarrayappend(materialsPriceJsonArr, materialPrice);
		append(materialArr,materialNr);
	}
	
	if(lineType == "ZK35"){
		priceGrp = get(row, "MVGR5");
		discount = get(row, "KBETR");
		jsonput(priceGrpAndDisc, "VPG"+priceGrp, discount);
		jsonarrayappend(PriceGroupsJsonArr, priceGrp);
		jsonarrayappend(PriceGroupsDscntJsonArr, discount);
		append(mpgArr,"VPG"+priceGrp);
	}	
	
	matArrStr = join(materialArr,",");
	mpgArrStr = join(mpgArr,",");
	
	jsonput(rowJsonObj, "MaterialScaleAndNFA", materialScaleAdNFAJson);
	jsonput(rowJsonObj, "PriceGrpAndDiscount", priceGrpAndDisc);
	jsonput(rowJsonObj, "MaterialScale", materialScaleJSON);
	jsonput(rowJsonObj, "MaterialNumbers", materialsJsonArr);
	jsonput(rowJsonObj, "MaterialPrice", materialsPriceJsonArr);
	jsonput(rowJsonObj, "PriceGroup", PriceGroupsJsonArr);
	jsonput(rowJsonObj, "PriceGroupDiscount", PriceGroupsDscntJsonArr);
	jsonput(rowJsonObj, "MatArrStr", matArrStr);
	jsonput(rowJsonObj, "MPGArrStr", mpgArrStr);
	jsonput(transactionsJsonObj, jsonKey, rowJsonObj);	
}

dspRecordKeys = jsonkeys(transactionsJsonObj);

for eachkey in dspRecordKeys{
	StrDict = dict("string");
	transactionNumber_t = "";
	id = "";
	eachDspRecord = jsonget(transactionsJsonObj, eachkey, "json");
	transactionName = jsonget(eachDspRecord, "TransactionName", "string");
	transactionType = jsonget(eachDspRecord, "TRANS_TYPE", "string");
	SalesOrg 	= jsonget(eachDspRecord, "SalesOrg", "string");
	distributnChannel = jsonget(eachDspRecord, "DistributionChannel", "string");
	customerNumber = jsonget(eachDspRecord, "CustomerNumber", "string");	
	
	validFrom = jsonget(eachDspRecord, "FromDate", "string");
	validTo = jsonget(eachDspRecord, "ToDate", "string");
	
	//Format validFrom and validTo date.
	validFrom = replace(validFrom,".","-");
	validTo = replace(validTo,".","-");
	
	mpgArr = split(jsonget(eachDspRecord, "MPGArrStr", "string"), ",");
	materialArr = split(jsonget(eachDspRecord, "MatArrStr", "string"), ",");

	bulkSearchArr = sort(mpgArr, "asc", "text");
	
	materialArr = sort(materialArr, "asc", "text");
	for eachMAT in materialArr{
		append(bulkSearchArr, eachMAT);
	}
	
	bulkSearchStr = join(bulkSearchArr, ",");

	//--------------> STEP 1:CREATE Transaction Start
	createUrl = "https://"+siteID+".bigmachines.com/rest/v1/commerceDocumentsOraclecpqoTransaction";
	currency = jsonget(eachDspRecord, "CURRENCY", "string");

	// Rest parameters
	jsonParams = json();
	jsonput(jsonParams,"_currency_pref",currency);

	params = jsontostr(jsonParams);

	// Rest Header
	headers = dict("string");  
	put(headers, "Content-Type", "application/json");

	results = urldatabypost(createUrl, params, "ERROR", headers, true);

	id = jsonget(json(results),"_id");
	result = "SUCCESS";

	if(find(results,"ERROR")<>-1){
		print "Step 1: Transaction creation failed";
		print id;
		
		put(StrDict, "BS_ID", id);
		put(StrDict, "TableName", tableName_t);
		put(StrDict, "Status", "Failure");
		put(StrDict, "Message", "Step 1: Transaction creation failed, CustNo - "+customerNumber+", From Dt-"+validFrom+", To Dt-"+validTo);
		put(StrDict, "Index", "0");		
		ret = util.transactionUploadStatusLog(StrDict);
		
		continue;
	}
	//STEP 1:CREATE Transaction End <-----------

	//--------------> STEP 1.1:Set Transaction attribute values start
	setTrnsAttributesURL = "https://"+siteID+".bigmachines.com/rest/v1/commerceDocumentsOraclecpqoTransaction/"+id+"/actions/apiSave_t";

	document = json();
	
	jsonput(document,"ntSelectedSalesOrg_t",SalesOrg);
	jsonput(document,"ntDistributionChannel_t",distributnChannel);	
	jsonput(document,"qq_nt_TransactionName_t",transactionName);
	jsonput(document,"qq_ntTransactionType_t",transactionType);
	jsonput(document,"qq_customerSearchField_t",customerNumber);
	jsonput(document,"pSDTransactionApprovalHistoryInfo_t",jsontostr(eachDspRecord));
	jsonput(document,"qq_MnlShpTo_Language_t","DE");	
		
	//user details
	jsonput(document,"owner_t",eNumber);
	jsonput(document,"adminSalesLogin_t",eNumber);
	jsonput(document,"preparedBySalesOrg_t",salesOrg);
	jsonput(document,"preparedByName_t",name1);
	jsonput(document,"createdBy_t",name1);
	jsonput(document,"preparedByPhone_t",telf1);
	jsonput(document,"preparedByEmail_t",email1);
	jsonput(document,"eRecordID_t",eRecID);
	jsonput(document,"preparedByTitle_t",title);
	
	jsonput(document,"quickItem_t",bulkSearchStr);
	
	// Rest parameters
	setTrnsAttrJsonParams = json();
	jsonput(setTrnsAttrJsonParams,"documents",document);
	setTrnsAttrParams = jsontostr(setTrnsAttrJsonParams);

	// Rest Header  
	put(headers, "Content-Type", "application/json");
	put(headers, "Authorization", auth_val);

	results = urldatabypost(setTrnsAttributesURL, setTrnsAttrParams, "ERROR", headers, true);
	if(find(results,"ERROR")<>-1){
		print "STEP 1.1: Transaction attribute values set failed";
		print id;
		
		put(StrDict, "BS_ID", id);
		put(StrDict, "TableName", tableName_t);
		put(StrDict, "Status", "Failure");
		put(StrDict, "Message", "STEP 1.1: Transaction attribute values set failed, CustNo - "+customerNumber+", From Dt-"+validFrom+", To Dt-"+validTo);
		put(StrDict, "Index", "0");		
		ret = util.transactionUploadStatusLog(StrDict);
		
		continue;
	}
	//STEP 1.1:Set Transaction attribute values End <--------------

	//--------------> STEP 2:Search Customer Start
	searchCustomerURL = "https://"+siteID+".bigmachines.com/rest/v1/commerceDocumentsOraclecpqoTransaction/"+id+"/actions/jetSearchCustomer_t";

	results = urldatabypost(searchCustomerURL, "", "ERROR", headers, true);
	if(find(results,"ERROR")<>-1){
		print "STEP 2: Customer search failed";
		print id;
		
		put(StrDict, "BS_ID", id);
		put(StrDict, "TableName", tableName_t);
		put(StrDict, "Status", "Failure");
		put(StrDict, "Message", "STEP 2: Customer search failed, CustNo - "+customerNumber+", From Dt-"+validFrom+", To Dt-"+validTo);
		put(StrDict, "Index", "0");		
		ret = util.transactionUploadStatusLog(StrDict);
		
		continue;
	}
	//STEP 2:Search Customer End <----------------

	//-------------> STEP 3:Select Customer from Arrayset Start
	selectCustURL = "https://"+siteID+".bigmachines.com/rest/v1/commerceDocumentsOraclecpqoTransaction/"+id+"/actions/nTSelectCustomer";
	results = urldatabypost(selectCustURL, "", "ERROR", headers, true);
	
	if(find(results,"ERROR")<>-1){
		print "STEP 3: Select Customer from Arrayset failed";
		print id;
		
		put(StrDict, "BS_ID", id);
		put(StrDict, "TableName", tableName_t);
		put(StrDict, "Status", "Failure");
		put(StrDict, "Message", "STEP 3: Select Customer from Arrayset failed, CustNo - "+customerNumber+", From Dt-"+validFrom+", To Dt-"+validTo);
		put(StrDict, "Index", "0");
		ret = util.transactionUploadStatusLog(StrDict);
		
		continue;
	}
	/*STEP 3:Select Customer from Arrayset End <-----------*/

	/*-------------> STEP 4:Add Customer Start */
	selectCustURL = "https://"+siteID+".bigmachines.com/rest/v1/commerceDocumentsOraclecpqoTransaction/"+id+"/actions/jetAddCustomer_t";
	results = urldatabypost(selectCustURL, "", "ERROR", headers, true);
	
	if(find(results,"ERROR")<>-1){
		print "STEP 4: Add Customer failed";
		print id;
		
		put(StrDict, "BS_ID", id);
		put(StrDict, "TableName", tableName_t);
		put(StrDict, "Status", "Failure");
		put(StrDict, "Message", "STEP 4: Add Customer failed, CustNo - "+customerNumber+", From Dt-"+validFrom+", To Dt-"+validTo);
		put(StrDict, "Index", "0");
		ret = util.transactionUploadStatusLog(StrDict);
		continue;
	}
	//STEP 4:Add Customer End <-----------

	//-------------> STEP 5:Save Transaction Start
	selectCustURL = "https://"+siteID+".bigmachines.com/rest/v1/commerceDocumentsOraclecpqoTransaction/"+id+"/actions/cleanSave_t";
	results = urldatabypost(selectCustURL, params, "ERROR", headers, true);
	
	if(find(results,"ERROR") == -1){
		transactionNumber_t = jsonget(json(results),"transactionNumber_t");
	}
	
	if(find(results,"ERROR")<>-1){
		print "STEP 5: First Save Transaction failed";
		print id;
		
		put(StrDict, "BS_ID", id);
		put(StrDict, "TableName", tableName_t);
		put(StrDict, "Status", "Failure");
		put(StrDict, "Message", "STEP 5: First Save Transaction failed, CustNo - "+customerNumber+", From Dt-"+validFrom+", To Dt-"+validTo);
		put(StrDict, "Index", "0");		
		ret = util.transactionUploadStatusLog(StrDict);

		continue;
	}
	//STEP 5:Save Transaction End <-----------

	//-------------> STEP 6:Bulk Search Materials and Pricing Groups Start
	document2 = json();
	jsonput(document2,"agreementValidFrom_t",validFrom); 
	jsonput(document2,"agreementValidTo_t",validTo);

	// Rest parameters
	setTrnsAttrJsonParams2 = json();
	jsonput(setTrnsAttrJsonParams2,"documents",document2);
	setTrnsAttrParams2 = jsontostr(setTrnsAttrJsonParams2);
	bulkSearchMaterialsURL = "https://"+siteID+".bigmachines.com/rest/v1/commerceDocumentsOraclecpqoTransaction/"+id+"/actions/addItems_t";
	
	results = urldatabypost(bulkSearchMaterialsURL, setTrnsAttrParams2, "ERROR", headers, true);
	
	if(find(results,"ERROR")<>-1){
		print "STEP 6: Bulk Search failed";
		print results;
		print transactionNumber_t;
		
		put(StrDict, "BS_ID", id);
		put(StrDict, "TransNum", transactionNumber_t);
		put(StrDict, "TableName", tableName_t);
		put(StrDict, "Status", "Failure");
		put(StrDict, "Message", "STEP 6: Bulk Search failed, CustNo - "+customerNumber+", From Dt-"+validFrom+", To Dt-"+validTo);
		put(StrDict, "Index", "0");		
		ret = util.transactionUploadStatusLog(StrDict);
		continue;
	}
	//STEP 6:Bulk Search Materials and Pricing Groups  End <-----------

	//-------------> STEP 9:Set Net Fixed Amount and Price Group discounts Start
	addSelectURL = "https://"+siteID+".bigmachines.com/rest/v1/commerceDocumentsOraclecpqoTransaction/"+id+"/actions/updateNFAAndMPGD_t";
		
	results = urldatabypost(addSelectURL, "", "ERROR", headers, true);

	if(find(results,"ERROR")<>-1){
		print "STEP 9: Set Net Fixed Amount and Discounts Failed";
		print transactionNumber_t;
		
		put(StrDict, "BS_ID", id);
		put(StrDict, "TransNum", transactionNumber_t);
		put(StrDict, "TableName", tableName_t);
		put(StrDict, "Status", "Failure");
		put(StrDict, "Message", "STEP 9: Set Net Fixed Amount and Discounts Failed, CustNo - "+customerNumber+", From Dt-"+validFrom+", To Dt-"+validTo);
		put(StrDict, "Index", "100");		
		ret = util.transactionUploadStatusLog(StrDict);
		continue;
	}
	//STEP 9:Set Net Fixed Amount and Price Group discounts End <-----------
	
	//-------------> STEP 10:Final Save Start
	selectCustURL = "https://"+siteID+".bigmachines.com/rest/v1/commerceDocumentsOraclecpqoTransaction/"+id+"/actions/cleanSave_t";
	results = urldatabypost(selectCustURL, params, "ERROR", headers, true);
	
	if(find(results,"ERROR")<>-1){
		print "STEP 9: Final Save Failed";
		print transactionNumber_t;
		
		put(StrDict, "BS_ID", id);
		put(StrDict, "TransNum", transactionNumber_t);
		put(StrDict, "TableName", tableName_t);
		put(StrDict, "Status", "Failure");
		put(StrDict, "Message", "STEP 9: Final Save Failed, CustNo - "+customerNumber+", From Dt-"+validFrom+", To Dt-"+validTo);
		put(StrDict, "Index", "100");		
		ret = util.transactionUploadStatusLog(StrDict);
		continue;
	}
	
	put(StrDict, "BS_ID", id);
	put(StrDict, "TransNum", transactionNumber_t);
	put(StrDict, "TableName", tableName_t);
	put(StrDict, "Status", "Success");
	put(StrDict, "Message", "Agreement Successfully created, CustNo - "+customerNumber+", From Dt-"+validFrom+", To Dt-"+validTo);
	put(StrDict, "Index", "999");		
	ret = util.transactionUploadStatusLog(StrDict);
	
	print transactionNumber_t;
	print "----------";
	//STEP 10:Final Save End <-----------
}
endDate = getdate();

//setattributevalue(1,"startTime_t", datetostr(startDate));
//setattributevalue(1,"endTim_te", datetostr(endDate));

startDateStr = datetostr(startDate, "dd-MM-yyyy HH:mm:ss");
endDateStr = datetostr(endDate, "dd-MM-yyyy HH:mm:ss");

StrDict2 = dict("string");
put(StrDict2, "BS_ID", startDateStr);
put(StrDict2, "TransNum", endDateStr);
put(StrDict2, "TableName", tableName_t);
put(StrDict2, "Status", "BatchComplete");
put(StrDict2, "Message", "Current Batch Processing complete");
put(StrDict2, "Index", "111");
ret = util.transactionUploadStatusLog(StrDict2);

return id;