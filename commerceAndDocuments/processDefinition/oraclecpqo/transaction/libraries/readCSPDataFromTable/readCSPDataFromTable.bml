siteID = _system_company_name;  //Expected value is eatontest2, eatontest4 etc.

//API Acess
apiUserID= "";
apiPassID= "";

salesENum="";
name1= "";
telf1= "";
email1="";
eRecID="";
eNumber = "";
validFrom = "";
validTo = "";
title = "";


// API Username and password is stroed in Site_Specific table so calling that table to fetch required information.
siteBMQL= bmql("SELECT FieldName,Value1 FROM Site_Specific WHERE SiteID = $siteID");
for eachSite in siteBMQL {
	apiUserID= get(eachSite,"FieldName");
	apiPassID= get(eachSite,"Value1");
}

//Basic authentication required in WebService API call.
auth_val = "Basic " + encodebase64(apiUserID + ":" + apiPassID);

resultSet = bmql ("SELECT SALES_DEAL_NUM,TRANS_NAME,KUNNR,KTOKD,VKORG,VTWEG,TRANS_TYPE,SPART,DATAB,DATBI,END_CUST,KSCHL,MVGR5,KBETR,KONWA,MATNR,ENUMBER,CURRENCY FROM CSP_24112020");

transactionsJsonObj = json();

for row in resultSet {
	materialScale = 0;
	rowJsonObj = json();
	materialScaleJSON = json();
	materialScaleAdNFAJson = json();
	priceGrpAndDisc = json();
	materialsJsonArr = jsonarray();
	materialsPriceJsonArr = jsonarray();
	PriceGroupsJsonArr = jsonarray();
	PriceGroupsDscntJsonArr = jsonarray();
	
	eNumber = get(row, "ENUMBER");
	
	//Get sales data from CUSMAS table and set it into different attribute for further use.
		
	if(salesENum == ""){
		salesRS=BMQL("SELECT SORT2, VKORG, SMTP_ADDR, NAME1, TELF1, KUNNR FROM CUSMAS WHERE SORT2=$eNumber");
		for eachUser in salesRS{		
			salesENum=get(eachUser, "SORT2");
			name1 = get(eachUser,"NAME1");
			telf1 = get(eachUser,"TELF1");
			email1 = get(eachUser, "SMTP_ADDR");
			eRecID = get(eachUser, "KUNNR");		
		}
		
		salesUserRS = bmql("Select Title from SalesTeams where eNumber=$eNumber");
		for salesUser in salesUserRS{
			title = get(salesUser, "Title");
		}
	}
	
	vistexNum = get(row, "SALES_DEAL_NUM");
	transName = get(row, "TRANS_NAME");
	customerNum = get(row, "KUNNR");
	endCustNum = get(row, "END_CUST");
	validFromDate = get(row, "DATAB");
	validToDate = get(row, "DATBI");
	
	jsonKey = vistexNum;
	
	if (NOT isnull(jsonget(transactionsJsonObj, jsonKey, "json"))){
		rowJsonObj = jsonget(transactionsJsonObj, jsonKey, "json");
		
		if(jsonarraysize(jsonget(rowJsonObj, "MaterialNumbers", "jsonarray")) > 0){
			materialsJsonArr 	= jsonget(rowJsonObj, "MaterialNumbers", "jsonarray");
			materialsPriceJsonArr 	= jsonget(rowJsonObj, "MaterialPrice", "jsonarray");
		}
		
		if(jsonarraysize(jsonget(rowJsonObj, "PriceGroup", "jsonarray")) > 0){
			PriceGroupsJsonArr 	= jsonget(rowJsonObj, "PriceGroup", "jsonarray");
			PriceGroupsDscntJsonArr = jsonget(rowJsonObj, "PriceGroupDiscount", "jsonarray");
		}
		
		if(NOT isjsonnull(rowJsonObj, "MaterialScale")){
			materialScaleJSON = jsonget(rowJsonObj, "MaterialScale", "json");
		}
		
		if(NOT isjsonnull(rowJsonObj, "MaterialScaleAndNFA")){
			materialScaleAdNFAJson = jsonget(rowJsonObj, "MaterialScaleAndNFA", "json");
		}
		
		if(NOT isjsonnull(rowJsonObj, "PriceGrpAndDiscount")){
			priceGrpAndDisc = jsonget(rowJsonObj, "PriceGrpAndDiscount", "json");
		}
	}
	
	jsonput(rowJsonObj, "TransactionName", transName + " - " + vistexNum);
	jsonput(rowJsonObj, "CustomerNumber", customerNum);
	jsonput(rowJsonObj, "EndCustNumber", endCustNum);
	jsonput(rowJsonObj, "CUST_TYPE", get(row, "KTOKD"));
	jsonput(rowJsonObj, "SalesOrg", get(row, "VKORG"));
	jsonput(rowJsonObj, "DistributionChannel", get(row, "VTWEG"));
	jsonput(rowJsonObj, "Division", get(row, "SPART"));
	jsonput(rowJsonObj, "FromDate", validFromDate);
	jsonput(rowJsonObj, "ToDate", validToDate);
	jsonput(rowJsonObj, "CURRENCY", get(row, "CURRENCY"));
	jsonput(rowJsonObj, "TRANS_TYPE", get(row, "TRANS_TYPE"));
	jsonput(rowJsonObj, "qq_MnlShpTo_Language_t", "DE");
	lineType = get(row, "KSCHL");
	if(lineType == "ZIDP"){
		materialNr = get(row, "MATNR");
		materialPrice = get(row, "KBETR");
		materialScale =	jsonget(materialScaleJSON, materialNr, "integer", 0);
		
		if(materialScale > 0){
			jsonput(materialScaleJSON, materialNr, materialScale+1);
		}else{
			jsonput(materialScaleJSON, materialNr, 1);
		}
		jsonput(materialScaleAdNFAJson, materialNr, materialPrice);
		
		jsonarrayappend(materialsJsonArr, materialNr);
		jsonarrayappend(materialsPriceJsonArr, materialPrice);
	}
	
	if(lineType == "ZIDO"){
		priceGrp = get(row, "MVGR5");
		discount = get(row, "KBETR");
		jsonput(priceGrpAndDisc, "VPG"+priceGrp, discount);
		jsonarrayappend(PriceGroupsJsonArr, get(row, "MVGR5"));
		jsonarrayappend(PriceGroupsDscntJsonArr, get(row, "KBETR"));
	}
	
	jsonput(rowJsonObj, "MaterialScaleAndNFA", materialScaleAdNFAJson);
	jsonput(rowJsonObj, "PriceGrpAndDiscount", priceGrpAndDisc);
	jsonput(rowJsonObj, "MaterialScale", materialScaleJSON);
	jsonput(rowJsonObj, "MaterialNumbers", materialsJsonArr);
	jsonput(rowJsonObj, "MaterialPrice", materialsPriceJsonArr);
	jsonput(rowJsonObj, "PriceGroup", PriceGroupsJsonArr);
	jsonput(rowJsonObj, "PriceGroupDiscount", PriceGroupsDscntJsonArr);
	jsonput(transactionsJsonObj, jsonKey, rowJsonObj);	
}
//print transactionsJsonObj;
dspRecordKeys = jsonkeys(transactionsJsonObj);
for eachkey in dspRecordKeys{
	//print eachkey;
	transactionNumber_t = "";
	id = "";
	eachDspRecord = jsonget(transactionsJsonObj, eachkey, "json");
	transactionName = jsonget(eachDspRecord, "TransactionName", "string");
	transactionType = jsonget(eachDspRecord, "TRANS_TYPE", "string");
	SalesOrg 		= jsonget(eachDspRecord, "SalesOrg", "string");
	distributnChannel = jsonget(eachDspRecord, "DistributionChannel", "string");
	customerNumber = jsonget(eachDspRecord, "CustomerNumber", "string");
	endCustNumber = jsonget(eachDspRecord, "EndCustNumber", "string");
	
	validFrom = jsonget(eachDspRecord, "FromDate", "string");
	validTo = jsonget(eachDspRecord, "ToDate", "string");
	
	//Format validFrom and validTo date.
	validFrom = replace(validFrom,".","-");
	validTo = replace(validTo,".","-");
	
	headers = dict("string"); 

	//--------------> STEP 1:CREATE Transaction Start
	createUrl = "https://"+siteID+".bigmachines.com/rest/v1/commerceDocumentsOraclecpqoTransaction";
	currency = jsonget(eachDspRecord, "CURRENCY", "string");

	// Rest parameters
	jsonParams = json();
	jsonput(jsonParams,"_currency_pref",currency);

	params = jsontostr(jsonParams);

	// Rest Header
	put(headers, "Content-Type", "application/json");

	results = urldatabypost(createUrl, params, "ERROR", headers, true);

	id = jsonget(json(results),"_id");
	result = "SUCCESS";

	if(find(results,"ERROR")<>-1){
		print "Step 1: Transaction creation failed";
		print id;
		continue;
	}
	//STEP 1:CREATE Transaction End <-----------

	//--------------> STEP 1.1:Set Transaction attribute values start
	setTrnsAttributesURL = "https://"+siteID+".bigmachines.com/rest/v1/commerceDocumentsOraclecpqoTransaction/"+id+"/actions/apiSave_t";

	document = json();
	
	jsonput(document,"ntSelectedSalesOrg_t",SalesOrg);
	jsonput(document,"ntDistributionChannel_t",distributnChannel);	
	jsonput(document,"qq_nt_TransactionName_t",transactionName);
	jsonput(document,"qq_ntTransactionType_t",transactionType);
	jsonput(document,"qq_customerSearchField_t",customerNumber);
	jsonput(document,"nt_endCustomerNo_t",endCustNumber); // Set End customer Search Param
	jsonput(document,"qq_nt_EndCust_country_t",""); // Set End customer Search Param
	jsonput(document,"nt_endCustomerType_search_t","Z340"); // Set End customer Search Param
	jsonput(document,"pSDTransactionApprovalHistoryInfo_t",jsontostr(eachDspRecord));
	
	//user details
	jsonput(document,"owner_t",eNumber);
	jsonput(document,"adminSalesLogin_t",eNumber);
	jsonput(document,"preparedBySalesOrg_t",salesOrg);
	jsonput(document,"preparedByName_t",name1);
	jsonput(document,"createdBy_t",name1);
	jsonput(document,"preparedByPhone_t",telf1);
	jsonput(document,"preparedByEmail_t",email1);
	jsonput(document,"eRecordID_t",eRecID);
	jsonput(document,"preparedByTitle_t",title);
	
	// Rest parameters
	setTrnsAttrJsonParams = json();
	jsonput(setTrnsAttrJsonParams,"documents",document);
	setTrnsAttrParams = jsontostr(setTrnsAttrJsonParams);

	// Rest Header  
	put(headers, "Content-Type", "application/json");
	put(headers, "Authorization", auth_val);

	results = urldatabypost(setTrnsAttributesURL, setTrnsAttrParams, "ERROR", headers, true);
	if(find(results,"ERROR")<>-1){
		print "STEP 1.1: Transaction attribute values set failed";
		print id;
		continue;
	}
	//STEP 1.1:Set Transaction attribute values End <--------------
	
	//--------------> STEP 2:Search Customer Start
	searchCustomerURL = "https://"+siteID+".bigmachines.com/rest/v1/commerceDocumentsOraclecpqoTransaction/"+id+"/actions/jetSearchCustomer_t";
	put(headers, "Content-Type", "application/json");
	put(headers, "Authorization", auth_val);
	
	results = urldatabypost(searchCustomerURL, "", "ERROR", headers, true);
	if(find(results,"ERROR")<>-1){
		print "STEP 2: Customer search failed";
		print id;
		continue;
	}
	//STEP 2:Search Customer End <----------------
	
	//--------------> STEP 2.1:Search End Customer Start
	searchCustomerURL = "https://"+siteID+".bigmachines.com/rest/v1/commerceDocumentsOraclecpqoTransaction/"+id+"/actions/search_EndCus_t";

	results = urldatabypost(searchCustomerURL, "", "ERROR", headers, true);
	if(find(results,"ERROR")<>-1){
		print "STEP 2: Customer search failed";
		print id;
		continue;
	}
	//STEP 2.1:Search End Customer End <----------------
	
	//-------------> STEP 3:Select Customer from Arrayset Start
	selectCustURL = "https://"+siteID+".bigmachines.com/rest/v1/commerceDocumentsOraclecpqoTransaction/"+id+"/actions/nTSelectCustomer";
	results = urldatabypost(selectCustURL, "", "ERROR", headers, true);
	
	if(find(results,"ERROR")<>-1){
		print "STEP 3: Select Customer from Arrayset failed";
		print id;
		continue;
	}
	//STEP 3:Select Customer from Arrayset End <-----------

	//-------------> STEP 4:Add Customer Start
	selectCustURL = "https://"+siteID+".bigmachines.com/rest/v1/commerceDocumentsOraclecpqoTransaction/"+id+"/actions/jetAddCustomer_t";
	results = urldatabypost(selectCustURL, "", "ERROR", headers, true);
	
	if(find(results,"ERROR")<>-1){
		print "STEP 4: Add Customer failed";
		print id;
		continue;
	}
	//STEP 4:Add Customer End <-----------
	
	//-------------> STEP 4.1:Add End Customer Start
	selectCustURL = "https://"+siteID+".bigmachines.com/rest/v1/commerceDocumentsOraclecpqoTransaction/"+id+"/actions/jetEndCust_t";
	results = urldatabypost(selectCustURL, "", "ERROR", headers, true);
	
	if(find(results,"ERROR")<>-1){
		print "STEP 4: Add End-Customer failed";
		print id;
		continue;
	}
	//STEP 4.1:Add End Customer End <-----------
	
	//-------------> STEP 5:Save Transaction Start
	selectCustURL = "https://"+siteID+".bigmachines.com/rest/v1/commerceDocumentsOraclecpqoTransaction/"+id+"/actions/cleanSave_t";
	results = urldatabypost(selectCustURL, params, "ERROR", headers, true);
	
	if(find(results,"ERROR") == -1){
		transactionNumber_t = jsonget(json(results),"transactionNumber_t");
	}
	
	if(find(results,"ERROR")<>-1){
		print "STEP 5: First Save Transaction failed";
		print id;
		continue;
	}
	//STEP 5:Save Transaction End <-----------
	
	//-------------> STEP 6:Bulk Search Materials and Pricing Groups Start
	document2 = json();
	jsonput(document2,"agreementValidFrom_t",validFrom); 
	jsonput(document2,"agreementValidTo_t",validTo);

	// Rest parameters
	setTrnsAttrJsonParams2 = json();
	jsonput(setTrnsAttrJsonParams2,"documents",document2);
	setTrnsAttrParams2 = jsontostr(setTrnsAttrJsonParams2);
	bulkSearchMaterialsURL = "https://"+siteID+".bigmachines.com/rest/v1/commerceDocumentsOraclecpqoTransaction/"+id+"/actions/searchMaterialAndPriceGroup_t";
		
	results = urldatabypost(bulkSearchMaterialsURL, setTrnsAttrParams2, "ERROR", headers, true);
	
	if(find(results,"ERROR")<>-1){
		print "STEP 6: Bulk Search failed";
		print transactionNumber_t;
		continue;
	}
	//STEP 6:Bulk Search Materials and Pricing Groups  End <-----------

	//-------------> STEP 7:Bulk Add Selected Materials and Pricing Groups Start
	bulkAddSelectURL = "https://"+siteID+".bigmachines.com/rest/v1/commerceDocumentsOraclecpqoTransaction/"+id+"/actions/addSelected_MaterialSearch";		
	results = urldatabypost(bulkAddSelectURL, "", "ERROR", headers, true);
	
	if(find(results,"ERROR")<>-1){
		print "STEP 7: Add Selected Materials and Pricing Groups Failed";
		print transactionNumber_t;
		continue;
	}
	//STEP 7:Bulk Add Selected Materials and Pricing Groups  End <-----------

	//-------------> STEP 8:Bulk Add to LIG Start
	addSelectURL = "https://"+siteID+".bigmachines.com/rest/v1/commerceDocumentsOraclecpqoTransaction/"+id+"/actions/addMaterialItemsWoGroups_t";
		
	results = urldatabypost(addSelectURL, "", "ERROR", headers, true);

	if(find(results,"ERROR")<>-1){
		print "STEP 8: Bulk Add to LIG Failed";
		print transactionNumber_t;
		continue;
	}
	//STEP 8:Bulk Add to LIG End <-----------
	
	//-------------> STEP 9:Set Net Fixed Amount and Price Group discounts Start
	addSelectURL = "https://"+siteID+".bigmachines.com/rest/v1/commerceDocumentsOraclecpqoTransaction/"+id+"/actions/updateNFAAndMPGD_t";
	results = urldatabypost(addSelectURL, "", "ERROR", headers, true);

	if(find(results,"ERROR")<>-1){
		print "STEP 9: Set Net Fixed Amount and Discounts Failed";
		print transactionNumber_t;
		continue;
	}
	//STEP 9:Set Net Fixed Amount and Price Group discounts End <-----------
	
	//-------------> STEP 10:Final Save Start
	selectCustURL = "https://"+siteID+".bigmachines.com/rest/v1/commerceDocumentsOraclecpqoTransaction/"+id+"/actions/cleanSave_t";
	results = urldatabypost(selectCustURL, params, "ERROR", headers, true);
	
	if(find(results,"ERROR")<>-1){
		print "STEP 10: Final Save Failed";
		print transactionNumber_t;
		continue;
	}
	print "----------------";
	print transactionNumber_t;
	print "----------";
	//STEP 10:Final Save End <-----------
}

return "";